plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.gradle.spotless' version '3.26.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "org.mikeneck.graalvm-native-image" version "0.1.1"
}

sourceCompatibility = 1.7
tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

sourceSets {
    main {
        java {
            srcDirs = ['java']
        }
        resources {
            srcDirs = ['java']
            include "**/*.afm"
        }
    }
}

mainClassName = 'com.gitlab.pdftk_java.pdftk'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.64'
}

nativeImage {
  mainClass = mainClassName
  executableName = 'pdftk'
  arguments(
      '-H:ResourceConfigurationFiles=META-INF/native-image/resource-config.json',
      '--no-fallback',
  )
}

spotless {
    java {
        target 'java/com/gitlab/pdftk_java/**/*.java'
        googleJavaFormat()
        paddedCell()
        removeUnusedImports()
    }
}
