plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '5.8.2'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "org.mikeneck.graalvm-native-image" version "0.8.0"
}

sourceCompatibility = 1.7
tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

sourceSets {
    main {
        java {
            srcDirs = ['java']
        }
        resources {
            srcDirs = ['java']
            include "**/*.afm"
            include "**/*.txt"
        }
    }
}

mainClassName = 'com.gitlab.pdftk_java.pdftk'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.64'
}

nativeImage {
  mainClass = mainClassName
  executableName = 'pdftk'
  arguments(
      '-H:ResourceConfigurationFiles=META-INF/native-image/resource-config.json',
      '--no-fallback',
      '-H:+AddAllCharsets',
      '-H:Log=registerResource:',
      '-H:GenerateDebugInfo=' + (project.hasProperty('generatedebuginfo') ? '1' : '0'),
  )
}

spotless {
    java {
        target 'java/com/gitlab/pdftk_java/**/*.java'
        googleJavaFormat()
        removeUnusedImports()
    }
}
